
%% Ott grammar for linear constraints work

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Metavariables  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

metavar x  ::= {{ com Term variables }}
metavar a  ::= {{ com Type variables }}
metavar K  ::= {{ com Data constructors }}
metavar T  ::= {{ com Type constructors }}
indexvar i, j ::=  {{ com Index variables }}
indexvar g, w, r, o, ext ::=  {{ com Annotation variables }}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Grammar  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

grammar

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Constraints  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Q :: 'Ct_' ::= {{ com Constraints }}
| Q1 & Q2            ::   :: With
| Q1 * Q2            ::   :: Tensor
| 1                  ::   :: Empty {{tex \mathbf{1}}}
| ! Q                ::   :: Unrestricted
| Q [ theta ]        :: M :: Subst
| pi . Q             :: M :: Scale  {{tex [[pi]]{⋅}[[Q]]}}

QQ {{ tex \mathcal{Q} }} :: 'G_' ::= {{ com Givens }}
| Q                ::   :: GivenConstraint
| QQ & Q           ::   :: With
| QQ1 * QQ2        ::   :: Tensor

C :: 'Ctg_' ::= {{ com Generated constraints}}
| Q                  ::   :: Simple
| C1 * C2            ::   :: Tensor
| C1 & C2            ::   :: With
| Q => C             ::   :: Impl  {{ tex [[Q]] ⊃ [[C]]}}
| ! C                ::   :: Unrestricted
| pi . C             :: M :: Scale  {{tex [[pi]]{⋅}[[C]]}}
| && C               :: M :: BigWith
| C [ theta ]        :: M :: Subst

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Types  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

t {{ tex \tau }}, u {{ tex \upsilon }}, s {{ tex \sigma}} :: 'Type_' ::= {{ com Types }}
| Q =o t                 ::   :: Qual
| t o= Q                 ::   :: Carry
| forall as . t          ::   :: ForallNested
  {{ tex [[forall]] [[as]]. [[t]] }}
| exists as . t          ::   :: ExistsNested
  {{ tex [[exists]] [[as]]. [[t]] }}
| ts1 -> t2              :: S :: NestedUrFunction
| ts1 ->_ pis t2           :: S :: Function  {{ tex [[ts1]] \to_{[[pis]]} [[t2]] }}
| T ts                   ::   :: TyConApp
| t [ theta ]            :: M :: Subst

ts {{ tex \overline{\tau} }}, us {{ tex \overline{\upsilon} }}, ss {{ tex \overline{\sigma} }}
  :: 'Types_' ::= {{ com List of types }}
| empty                  ::   :: None
| t                      ::   :: One
| ts1 , .... , tsi       ::   :: Many
| as                     :: M :: Vars

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Multiplicities  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

pi {{ tex \pi }} :: 'Mult_' ::= {{ com Multiplicities }}
  | 1                    ::   :: One
  | omega                ::   :: Many
  % I'd like to avoid multiplicity variables if we can, as they are
  % not very relevant to our discussion

pis {{ tex \overline{\pi} }} :: 'Mults_' ::= {{ com Lists of multiplicities }}
| empty                  ::   :: None
| pi                     ::   :: One
| pis1 , .... , pisi      ::   :: Many

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Substitutions  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

theta {{ tex \theta }} :: 'Subst_' ::= {{ com Substitutions }}
| ts / as                ::   :: Mappings

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Expressions  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

e :: 'Expr_' ::= {{ com Expressions }}
| x                      ::   :: Var
| \ x . e                ::   :: Lam
| e1 e2                  ::   :: App
| pack e                 ::   :: Pack
| unpack x = e1 in e2    ::   :: Unpack
| let x = e1 in e2       ::   :: Let
| case e of { alts }     ::   :: Case

alt :: 'Alt_' ::= {{ com Case alternative }}
| K xs -> e              ::   :: Alt

alts {{ tex \overline{\ottmv{K}\ \overline{\ottmv{x} } \to \ottnt{e} } }}
  :: 'Alts_' ::= {{ com Case alternatives }}
| empty                  ::   :: None
| alt                    ::   :: One
| alts1 ; .... ; altsi   ::   :: Many

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Variables  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

xs {{ tex \overline{\ottmv{x} } }} :: 'Termvars_' ::=
| empty                  ::   :: None
| x                      ::   :: One
| xs1 , .... , xsi       ::   :: Many

as {{ tex \overline{\ottmv{a} } }} :: 'Tyvars_' ::=
| empty                  ::   :: None
| a                      ::   :: One
| as1 , .... , asi       ::   :: Many

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Contexts  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

binding :: 'Binding_' ::= {{ com Context binding }}
| x :_ pi t           ::    :: Bnd
  {{ tex [[x]]{:}_{[[pi]]}[[t]] }}

G {{ tex \Gamma }} :: 'Ctx_' ::= {{ com Contexts }}
| empty             ::   :: None
| binding           ::   :: One
| G1 , .... , Gi    ::   :: Many
| G1 + G2           :: M :: Add
| Sum G2            :: M :: BigSum
| pi . G            :: M :: Scale {{ tex [[pi]]{⋅}[[G]]}}
| < binding >       :: M :: Repeat
  {{ tex \overline{[[binding]]} }}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Terminals  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

terminals :: 'terminals_' ::=
| &           ::   :: With    {{ tex \aand }}
| &&          ::   :: BigWith {{ tex \bigaand }}
| *           ::   :: Tensor  {{ tex \otimes }}
| |-          ::   :: entails {{ tex \vdash }}
| |->         ::   :: infer {{ tex \vdashi }}
| |-s         ::   :: solve {{ tex \vdashs }}
| ||-         ::   :: Entails {{ tex \Vdash }}
| ~>          ::   :: generates {{ tex \leadsto }}
| \           ::   :: Lambda  {{ tex \lambda }}
| ->          ::   :: Arrow   {{ tex \to }}
| forall      ::   :: Forall  {{ tex \forall }}
| exists      ::   :: Exists  {{ tex \exists }}
| =o          ::   :: Lolli   {{ tex \Lolly }}
| o=          ::   :: illoL   {{ tex \RLolly }}
| \in         ::   :: In      {{ tex \in }}
| omega       ::   :: Many    {{ tex \omega }}
| Sum         ::   :: BigSum  {{ tex \sum }}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Formulae  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

formula :: formula_ ::=
| judgement                       ::   :: judgement
| formula \\\\                    ::   :: lbh
  {{ tex [[formula]] \ottlinebreakhack }}
  % See ottalt, sec. 1.1
| formula1 .... formulai          ::   :: dots
| binding \in G                   ::   :: in_G
| freshness condition on as       ::   :: freshness
  {{ tex \text{freshness condition on } [[as]] }}
| as fresh                        ::   :: fresh
  {{ tex [[as]] \text{ fresh} }}
| < formula >                     ::   :: Repeat
  {{ tex \overline{[[formula]]} }}
| K : t                           ::   :: DataconTy

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Parsing rules  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

parsing

Type_ExistsNested <= Type_Carry
Ct_Tensor <= Ct_Unrestricted
Ct_Tensor <= Ct_Scale
Ctg_Tensor <= Ctg_Unrestricted
Ctg_Tensor <= Ctg_Scale
Ctx_Add <= Ctx_Scale
